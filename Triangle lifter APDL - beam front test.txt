! ---------------------------------------------------------------------------------
! TRIANGLE LIFTER - kassow arm lateral test
! ...............................................................
! DETERMINATION OF MAXIMUM DISPLACEMENT AND STRESS
! ---------------------------------------------------------------------------------

FINISH
/CLEAR, START, NEW
/FILNAM,TRIANGLE_LIFTER
/TITLE,TRIANGLE LIFTER

! >>>>> MODEL PARAMETERS <<<<<

L1 = 444 	! Link length
S = 17		! Distance from front and back link
B = 35		! Section thickness of the link
H = 13		! Section hight of the link
SL_th = 0.8	! Spiral lifter thickness
R2 = 75/2	! Outer radius of the spiral lift
R1 = R2 - SL_th	! Inner radius of the spiral lift
B_B = 15	! Section thickness of the edge block
H_B = 30	! Section hight of the edge block
TH_plate = 6	! Top plate thickness
W1 = 13
W2 = 13
W3 = 35
t1 = 5.5
t2 = 5.5
t3 = 5
PS = 27			! Top Plate Shift
HP = 1552		! Platform height
Spir_base = 145 	! Spiralift base height
Spir_pos = -195 	! Spiralift position

! Worst static case
W_K = 40*9.81				! Robot total weight force
C_K = 620				! CoM of the robot
F_K = 5*9.81				! End effector payload
L_K = 62+1800+100			! End effector extension
H_K = 200				! kassow base length
B_K = 160				! kassow base width
J_K = 391.2				! Arm height from the base
D_K = 223				! Distance of the base from RF13
E_K = 80				! Distance of the first joint from the front side of the base

F = (4.2+0.75+0.175)*9.81
FB = 4.8*9.81
L = 1648
LB = 673
EQ_force = (F*L+FB*LB)/L

! Datasheet load
F_DS = 1000	    	! N
M_DS = 500000	    	! Nmm
EQ_F_DS = M_DS/L_K	! N

x_shift = -5.25
z_shift = -19.593
N = 6		! Number of stages

*AFUN, DEG				! To set the angles in degrees
alpha = ASIN((HP-PS-TH_plate)/N/L1)	! Inclination angle of links wrt horizontal plane
beta_os = 30				! Beta offset to get the reference position along the symmetry line
beta = 0				! Angle at which the momentum is applied (wrt y axis of csys,0)

x1 = L1/2*COS(alpha)	
y1 = L1/2*SIN(alpha)
x2 = L1*COS(alpha)	
y2 = L1*SIN(alpha)

! >>>>> MATERIAL PROPERTIES <<<<<

E_al = 68900		! Aluminum 6082 Young modulus (MPa)
ni_al = 0.33		! Aluminum 6082 Poisson's ratio (-)
Density_al = 2.7e-6	! Aluminum 6082 density (kg/mm^3)

E_ss = 193000 	 	! Stainless steel 301 Young's modulus (MPa) 193000 
ni_ss = 0.27		! Stainless steel 301 Poisson's ratio (-)
Density_ss = 7.88e-6	! Stainless steel 301 density (kg/mm^3)

! >>>>> OTHER PARAMETERS <<<<<

E_length = 15		! Element length (mm)
E_plate = 10		! Top plate element size (mm)
eps = 1E-4		! Selection tolerance
eps_2 = S + 1 		! Selection tolerance for links

/PREP7

! Types of elements
ET,1,BEAM189		! Structure beams
ET,2,184,1,1		! Rigid beams
ET,3,SOLID187		! Top plate (10-node element)
ET,4,184,1,1		! Rigid beams

!cross-sectional data of simplified links
SECTYPE,1,BEAM,RECT, link
SECDATA,B,H

!cross-sectional data of spiral lift
SECTYPE,2,BEAM,CTUBE, spiral, 5
SECDATA,R1,R2, 30

!cross-sectional data of edge blocks
SECTYPE,3,BEAM,RECT, edge
SECDATA,B_B,H_B

!cross-sectional data of top plate
SECTYPE,4,SHELL
SECDATA,TH_plate 

!cross-sectional data of links
SECTYPE,5,BEAM,CHAN, C_link
SECDATA,W1,W2,W3,t1,t2,t3

! Coordinate systems definition
LOCAL, 11, 0, -19.593, , -33.937, 0, 0, 60							! Coordinate system of the second side
LOCAL, 12, 0, 19.593 + x2, , -33.937, 0, 0, 120							! Coordinate system of the third side
LOCAL, 13, 0,,,-22.624, 0, 0, -60								! Coordinate system of the first edge
LOCAL, 14, 0,195.053*SIN(60),y2*N+PS+TH_plate+40,-22.624-195.053*COS(60),0,0,beta_os+beta	! Coordinate system of the arm

! Aluminum 6082 properties
MP, EX  , 1, E_al
MP, PRXY, 1, ni_al
MP, dens, 1, Density_al

! Stainless steel 316L properties
MP, EX  , 2, E_ss
MP, PRXY, 2, ni_ss
MP, dens, 2, Density_ss

! >>>>> SIDE 1 <<<<<

csys, 0

*DO, i, 1, N,1

K,700, 0 , 2000, 0
K,750, x2, - 2000, S

inc_K = (i-1)*6
inc_y = (i-1)*y2
inc_L = (i-1)*6
inc_mesh = (i-1)*4

K, 1 + inc_K, 0 , 0  + inc_y, 0
K, 2 + inc_K, x1, y1 + inc_y, 0
K, 3 + inc_K, x2, y2 + inc_y, 0
L, 1 + inc_L, 2 + inc_L
L, 2 + inc_L, 3 + inc_L
LESIZE, all,E_length
LATT,1,,1,,700,,5
LMESH, 1 + inc_mesh, 2 + inc_mesh
Lsel,u,line,,all

K, 4 + inc_K, x2, 0  + inc_y, S
K, 5 + inc_K, x1, y1 + inc_y, S
K, 6 + inc_K, 0 , y2 + inc_y, S
L, 4 + inc_L, 5 + inc_L
L, 5 + inc_L, 6 + inc_L
LESIZE, all,E_length
LATT,1,,1,,750,,5
LMESH, 3 + inc_mesh, 4 + inc_mesh
Lsel,u,line,,all

*ENDDO

!Internal pins - SIDE 1
NSEL, S, LOC, X, - eps, esp
NSEL, A, LOC, X, x1 - eps, x1 + esp
NSEL, A, LOC, X, x2 - eps, x2 + esp
CPINTF,ux,eps_2
CPINTF,uy,eps_2
CPINTF,uz,eps_2
CPINTF,rotx,eps_2
CPINTF,roty,eps_2

! >>>>> SIDE 2 <<<<<

csys, 11

*DO, i, 1, N,1

K,500, 0 , - 2000, -S
K,550, x2, 2000, 0

inc_side = 6*N 
inc_K = (i-1)*6
inc_y = (i-1)*y2
inc_L = (i-1)*6
inc_mesh = (i-1)*4
inc_side_m = 4*N

K, 1 + inc_K + inc_side, x2, 0  + inc_y, 0
K, 2 + inc_K + inc_side, x1, y1 + inc_y, 0
K, 3 + inc_K + inc_side, 0 , y2 + inc_y, 0
L, 1 + inc_L + inc_side, 2 + inc_L + inc_side
L, 2 + inc_L + inc_side, 3 + inc_L + inc_side
LESIZE, all,E_length
LATT,1,,1,,550,,5
LMESH, 1 + inc_mesh + inc_side_m, 2 + inc_mesh + inc_side_m
Lsel,u,line,,all

K, 4 + inc_K + inc_side, 0 , 0  + inc_y, -S
K, 5 + inc_K + inc_side, x1, y1 + inc_y, -S
K, 6 + inc_K + inc_side, x2, y2 + inc_y, -S
L, 4 + inc_L + inc_side, 5 + inc_L + inc_side
L, 5 + inc_L + inc_side, 6 + inc_L + inc_side
LESIZE, all,E_length
LATT,1,,1,,500,,5
LMESH, 3 + inc_mesh + inc_side_m, 4 + inc_mesh + inc_side_m
Lsel,u,line,,all

*ENDDO

!Internal pins - SIDE 2
NSEL, S, LOC, X, - eps, esp
NSEL, A, LOC, X, x1 - eps, x1 + esp
NSEL, A, LOC, X, x2 - eps, x2 + esp
NSEL, R, LOC, Z, -S - eps, esp
NSEL, U, LOC, Y, -5, 5
NROTAT, ALL, LOCAL              ! Rotate all nodes to align with the local system
CPINTF,ux,eps_2
CPINTF,uy,eps_2
CPINTF,uz,eps_2
CPINTF,rotx,eps_2
CPINTF,roty,eps_2

! >>>>> SIDE 3 <<<<<

csys, 12

*DO, i, 1, N,1

K,600, 0 , - 3000, S
K,650, x2, 3000, 0

inc_side = 6*N*2 
inc_K = (i-1)*6
inc_y = (i-1)*y2
inc_L = (i-1)*6
inc_mesh = (i-1)*4
inc_side_m = 4*N*2

K, 1 + inc_K + inc_side, 0 , 0  + inc_y, 0
K, 2 + inc_K + inc_side, x1, y1 + inc_y, 0
K, 3 + inc_K + inc_side, x2, y2 + inc_y, 0
L, 1 + inc_L + inc_side, 2 + inc_L + inc_side
L, 2 + inc_L + inc_side, 3 + inc_L + inc_side
LESIZE, all,E_length
LATT,1,,1,,650,,5
LMESH, 1 + inc_mesh + inc_side_m, 2 + inc_mesh + inc_side_m
Lsel,u,line,,all

K, 4 + inc_K + inc_side, x2, 0  + inc_y, S
K, 5 + inc_K + inc_side, x1, y1 + inc_y, S
K, 6 + inc_K + inc_side, 0 , y2 + inc_y, S
L, 4 + inc_L + inc_side, 5 + inc_L + inc_side
L, 5 + inc_L + inc_side, 6 + inc_L + inc_side
LESIZE, all,E_length
LATT,1,,1,,600,,5
LMESH, 3 + inc_mesh + inc_side_m, 4 + inc_mesh + inc_side_m
Lsel,u,line,,all

*ENDDO

!Internal pins - SIDE 3
NSEL, S, LOC, X, - eps, esp
NSEL, A, LOC, X, x1 - eps, x1 + esp
NSEL, A, LOC, X, x2 - eps, x2 + esp
NSEL, R, LOC, Z, -eps, S + esp
NSEL, U, LOC, Y, -5, 5
NROTAT, ALL, LOCAL              ! Rotate all nodes to align with the local system
CPINTF,ux,eps_2
CPINTF,uy,eps_2
CPINTF,uz,eps_2
CPINTF,rotx,eps_2
CPINTF,roty,eps_2

! >>>>> EDGE BLOCKS <<<<<

*DO, i, 1, N+1,1

csys, 0
K,3000 + 2*(i-1)    , 0  , y2*(i-1), -4-H/2
K,3000 + 2*(i-1) + 1, x2 , y2*(i-1), -4-H/2

csys, 11
K,3100 + 2*(i-1)    , 0  , y2*(i-1), 4+H/2
K,3100 + 2*(i-1) + 1, x2 , y2*(i-1), 4+H/2

csys, 12
K,3200 + 2*(i-1)    , 0  , y2*(i-1), -4-H/2
K,3200 + 2*(i-1) + 1, x2 , y2*(i-1), -4-H/2

L, 3000 + 2*(i-1), 3100 + 2*(i-1)
L, 3001 + 2*(i-1), 3200 + 2*(i-1)
L, 3101 + 2*(i-1), 3201 + 2*(i-1)

LESIZE, all,E_length
LATT,2,,1,,,,3
LMESH, all
Lsel,u,line,,all

*ENDDO

! >>>>> EDGE LINKS <<<<<

! Side 1 left node
csys, 0
KSEL,S,LOC,X,x2
KSEL,R,LOC,Y,y2
KSEL,R,LOC,Z,0
*GET, S1_L, KP, 0, NUM, MIN

! Side 1 right node
KSEL,S,LOC,X,0
KSEL,R,LOC,Y,y2
KSEL,R,LOC,Z,0
*GET, S1_R, KP, 0, NUM, MIN

! Side 2 left node
csys, 11
KSEL,S,LOC,X,x2
KSEL,R,LOC,Y,y2
KSEL,R,LOC,Z,0
*GET, S2_L, KP, 0, NUM, MIN

! Side 2 right node
KSEL,S,LOC,X,0
KSEL,R,LOC,Y,y2
KSEL,R,LOC,Z,0
*GET, S2_R, KP, 0, NUM, MIN

! Side 3 left node
csys, 12
KSEL,S,LOC,X,x2
KSEL,R,LOC,Y,y2
KSEL,R,LOC,Z,0
*GET, S3_L, KP, 0, NUM, MIN

! Side 3 right node
KSEL,S,LOC,X,0
KSEL,R,LOC,Y,y2
KSEL,R,LOC,Z,0
*GET, S3_R, KP, 0, NUM, MIN

allsel,all

*DO, i, 1, N,1

csys, 0
K,3300 + 2*(i-1)    , 0  , y2*i, -H/2
K,3300 + 2*(i-1) + 1, x2 , y2*i, -H/2

csys, 11
K,3400 + 2*(i-1)    , 0  , y2*i, H/2
K,3400 + 2*(i-1) + 1, x2 , y2*i, H/2

csys, 12
K,3500 + 2*(i-1)    , 0  , y2*i, -H/2
K,3500 + 2*(i-1) + 1, x2 , y2*i, -H/2

L, 3301 + 2*(i-1), S1_L + 6*(i-1)
L, 3400 + 2*(i-1), S2_R + 6*(i-1)
L, 3501 + 2*(i-1), S3_L + 6*(i-1)

*IF, i, LT, N, THEN

 L, 3300 + 2*(i-1), S1_R + 6*(i-1)
 L, 3401 + 2*(i-1), S2_L + 6*(i-1)
 L, 3500 + 2*(i-1), S3_R + 6*(i-1)

*ENDIF

TYPE, 2
LESIZE, all,E_length
LMESH, all
allsel,all

*ENDDO

L, 3300 + 2*(N-1)    , 6*N
L, 3400 + 2*(N-1) + 1, 6*N*2
L, 3500 + 2*(N-1)    , 6*N*3

TYPE, 2
LESIZE, all,E_length
LMESH, all
allsel,all

csys, 0
K,3300 + 2*N    , 0  , 0, -H/2
K,3300 + 2*N + 1, x2 , 0, -H/2

csys, 11
K,3400 + 2*N    , 0  , 0, H/2
K,3400 + 2*N + 1, x2 , 0, H/2

csys, 12
K,3500 + 2*N    , 0  , 0, -H/2
K,3500 + 2*N + 1, x2 , 0, -H/2

! First bottom edge
L, 3300 + 2*N, 1
L, 3400 + 2*N, 6*(N+1)-2

!Second bottom edge
L, 3400 + 2*N+1, 6*N+1
L, 3500 + 2*N+1, 2*N*6+4

! Third bottom edge
L, 3300 + 2*N+1, 4
L, 3500 + 2*N  , 2*N*6+1

TYPE, 2
LESIZE, all,E_length
LMESH, all
allsel,all

! Side 1 connections to edges
csys, 0
NSEL, S, LOC, Z, -H/2-4-eps, -H/2+eps
NSEL, U, LOC, X, eps, x2-eps
NROTAT, ALL, LOCAL              ! Rotate all nodes to align with the local system
CPINTF,ux,eps_2
CPINTF,uy,eps_2
CPINTF,uz,eps_2
CPINTF,rotx,eps_2
CPINTF,roty,eps_2

! Side 2 connections to edges
csys, 11
NSEL, S, LOC, Z, H/2-eps, H/2+4+eps
NSEL, U, LOC, X, eps, x2-eps
NROTAT, ALL, LOCAL              ! Rotate all nodes to align with the local system
CPINTF,ux,eps_2
CPINTF,uy,eps_2
CPINTF,uz,eps_2
CPINTF,rotx,eps_2
CPINTF,roty,eps_2

! Side 3 connections to edges
csys, 12
NSEL, S, LOC, Z, -4-H/2-eps, -H/2+eps
NSEL, U, LOC, X, eps, x2-eps
NROTAT, ALL, LOCAL              ! Rotate all nodes to align with the local system
CPINTF,ux,eps_2
CPINTF,uy,eps_2
CPINTF,uz,eps_2
CPINTF,rotx,eps_2
CPINTF,roty,eps_2

! >>>>> SPIRAL LIFT <<<<<

csys, 13
k, 1000, 0, Spir_base, Spir_pos
k, 1001, 0, y2*N     , Spir_pos !
K, 900,  0, 0, 0
L, 1000, 1001
LESIZE, all, E_length
LATT,2,,1,,900,,2
LMESH, all
Lsel,u,line,,all

NSEL, S, LOC, X, -eps, eps
NSEL, R, LOC, Z, Spir_pos - eps, Spir_pos + eps
NROTAT, ALL, LOCAL              ! Rotate all nodes to align with the local system

! >>>>> GET NUMBER OF EDGE NODES <<<<<

! Edge 1
csys,0
NSEL,S,LOC,X,x_shift
NSEL,R,LOC,Y,y2*N
NSEL,R,LOC,Z,z_shift
*GET, ED1, NODE, 0, NUM, MIN

! Edge 2
csys,11
NSEL,S,LOC,X,x2 - x_shift
NSEL,R,LOC,Y,y2*N
NSEL,R,LOC,Z,-z_shift
*GET, ED2, NODE, 0, NUM, MIN

! Edge 3
csys,12
NSEL,S,LOC,X,x_shift
NSEL,R,LOC,Y,y2*N
NSEL,R,LOC,Z,z_shift
*GET, ED3, NODE, 0, NUM, MIN

! Spiralift
csys, 13
NSEL,S,LOC,X,0
NSEL,R,LOC,Y,N*y2
NSEL,R,LOC,Z,Spir_pos
*GET, SL, NODE, 0, NUM, MIN  ! Spiral lift top node

allsel,all

! >>>>> TOP PLATE <<<<<

csys, 13
K, 2001, 60  , y2*N+PS, 28.847 
K, 2002, 265 , y2*N+PS, -89.51
K, 2003, 325 , y2*N+PS, -193.433
K, 2004, 325 , y2*N+PS, -334.151
K, 2005, 225 , y2*N+PS, -434.053
K, 2006, -225, y2*N+PS, -434.053
K, 2007, -325, y2*N+PS, -334.151
K, 2008, -325, y2*N+PS, -193.433
K, 2009, -265, y2*N+PS, -89.51
K, 2010, -60 , y2*N+PS, 28.847
 
! Create the keypoints of the kassow base
K, 2020,  B_K/2, y2*N+PS, -D_K 
K, 2021,  B_K/2, y2*N+PS, -D_K-H_K 
K, 2022, -B_K/2, y2*N+PS, -D_K-H_K 
K, 2023, -B_K/2, y2*N+PS, -D_K 

! Create the keypoints for better mesh
K, 2024,  B_K/2, y2*N+PS, -434.053 
K, 2025, -B_K/2, y2*N+PS, -434.053

! Create the areas of the top plate

! Area 1
A,2001,2002,2020,2023,2009,2010

! Area 2
A,2002,2003,2004,2005,2024,2021,2020

! Area 3
A,2024,2025,2022,2021

! Area 4
A,2006,2007,2008,2009,2023,2022,2025

! Area 5
A,2020,2021,2022,2023

! Create a line for extrusion
K, 4001, 60, y2*N+PS+TH_plate, 28.847
L, 2001, 4001

! Extrude along the line
*get,extrude_line,line,,num,max
Lesize,extrude_line,,,2
Vdrag,all,,,,,,extrude_line

! Mesh the area with shell elements
TYPE, 3
MAT, 1
ESIZE,E_plate           ! Set element size
mshape,1		! Mesh type: tetrahedral-shaped elements
mshk,0			! Free meshing
VMESH,ALL               ! Mesh all volumes
allsel,all

! TOP PLATE CONSTRAINTS

! Top Edge 1 constraint     -----------------------------------------------------------------------------------------------------------------------------------

csys, 13
NSEL,S, LOC, X, -10, 10
NSEL,R, LOC, Z, 0, 20
NSEL,R, LOC, Y, y2*N+PS-esp, y2*N+PS+esp
NSEL,U, LOC, Y, y2*N+PS+0.1, y2*N+PS+1000

RBE3, ED1, ALL, ALL	! Distributes the force/moment applied at the master node to a set of slave nodes

! Top Edge 2 constraint     -----------------------------------------------------------------------------------------------------------------------------------

csys, 11
*GET, NX, NODE, ED2, LOC, X   ! Get X coordinate of edge 2
*GET, NZ, NODE, ED2, LOC, Z   ! Get Z coordinate of edge 2

N, NODE, NX, y2*N, NZ
N, NODE, NX, y2*N+5, NZ

NSEL,S, LOC, X, NX-eps, NX+eps
NSEL,R, LOC, Z, NZ-eps, NZ+eps
NSEL,R, LOC, Y, y2*N-1, y2*N+1
*GET, ED2_constr_1, NODE, 0, NUM, MAX
NROTAT, ALL, LOCAL              ! Rotate all nodes to align with the local system

NSEL,S, LOC, X, NX-eps, NX+eps
NSEL,R, LOC, Z, NZ-eps, NZ+eps
NSEL,R, LOC, Y, y2*N+5-1, y2*N+5+1
*GET, ED2_constr_2, NODE, 0, NUM, MAX
NROTAT, ALL, LOCAL              ! Rotate all nodes to align with the local system

TYPE, 2
E, ED2_constr_1, ED2_constr_2

NSEL,S, node,, ED2
NROTAT, ALL, LOCAL              ! Rotate all nodes to align with the local system

! Select the nodes of the top plate to connect with the edge
NSEL,S, LOC, X, NX-20, NX+20
NSEL,R, LOC, Z, NZ-10, NZ+10
NSEL,R, LOC, Y, y2*N+PS-esp, y2*N+PS+esp
NSEL,U, LOC, Y, y2*N+PS+0.1, y2*N+PS+1000
NSEL,U, LOC, Y, y2*N+PS-1000, y2*N+PS-0.1

NROTAT, ALL, LOCAL              ! Rotate all nodes to align with the local system

RBE3, ED2_constr_2, uy, ALL	! Distributes the force/moment applied at the master node to a set of slave nodes
RBE3, ED2_constr_2, uz, ALL
RBE3, ED2_constr_2, rotx, ALL
RBE3, ED2_constr_2, roty, ALL
RBE3, ED2_constr_2, rotz, ALL

allsel,all

CP, NEXT, ux,   ED2, ED2_constr_1
CP, NEXT, uy,   ED2, ED2_constr_1
CP, NEXT, uz,   ED2, ED2_constr_1
CP, NEXT, rotx, ED2, ED2_constr_1
CP, NEXT, roty, ED2, ED2_constr_1
CP, NEXT, rotz, ED2, ED2_constr_1

! Top Edge 3 constraint  -----------------------------------------------------------------------------------------------------------------------------------

csys, 0
*GET, NX, NODE, ED3, LOC, X   ! Get X coordinate of edge 3
*GET, NZ, NODE, ED3, LOC, Z   ! Get Z coordinate of edge 3

N, NODE, NX, y2*N, NZ
N, NODE, NX, y2*N+5, NZ

NSEL,S, LOC, X, NX-eps, NX+eps
NSEL,R, LOC, Z, NZ-eps, NZ+eps
NSEL,R, LOC, Y, y2*N-1, y2*N+1
*GET, ED3_constr_1, NODE, 0, NUM, MAX
NROTAT, ALL, LOCAL              ! Rotate all nodes to align with the local system

NSEL,S, LOC, X, NX-eps, NX+eps
NSEL,R, LOC, Z, NZ-eps, NZ+eps
NSEL,R, LOC, Y, y2*N+5-1, y2*N+5+1
*GET, ED3_constr_2, NODE, 0, NUM, MAX
NROTAT, ALL, LOCAL              ! Rotate all nodes to align with the local system

TYPE, 2
E, ED3_constr_1, ED3_constr_2

NSEL,S, node,, ED3
NROTAT, ALL, LOCAL              ! Rotate all nodes to align with the local system

! Select the nodes of the top plate to connect with the edge
NSEL,S, LOC, X, NX-20, NX+20
NSEL,R, LOC, Z, NZ-10, NZ+10
NSEL,R, LOC, Y, y2*N+PS-esp, y2*N+PS+esp
NSEL,U, LOC, Y, y2*N+PS+0.1, y2*N+PS+1000
NSEL,U, LOC, Y, y2*N+PS-1000, y2*N+PS-0.1

NROTAT, ALL, LOCAL              ! Rotate all nodes to align with the local system

RBE3, ED3_constr_2, uy, ALL	! Distributes the force/moment applied at the master node to a set of slave nodes
RBE3, ED3_constr_2, uz, ALL
RBE3, ED3_constr_2, rotx, ALL
RBE3, ED3_constr_2, roty, ALL
RBE3, ED3_constr_2, rotz, ALL

allsel,all

CP, NEXT, ux,   ED3, ED3_constr_1
CP, NEXT, uy,   ED3, ED3_constr_1
CP, NEXT, uz,   ED3, ED3_constr_1
CP, NEXT, rotx, ED3, ED3_constr_1
CP, NEXT, roty, ED3, ED3_constr_1
CP, NEXT, rotz, ED3, ED3_constr_1

! Spiral lift constraint  -----------------------------------------------------------------------------------------------------------------------------------

csys, 13
*GET, NX, NODE, SL, LOC, X   ! Get X coordinate of node SL
*GET, NZ, NODE, SL, LOC, Z   ! Get Z coordinate of node SL

! Create two other nodes for the spherical constraint
N, NODE, NX, y2*N, NZ
N, NODE, NX, y2*N+5, NZ

NSEL,S, LOC, X, NX-eps, NX+eps
NSEL,R, LOC, Z, NZ-eps, NZ+eps
NSEL,R, LOC, Y, y2*N-1, y2*N+1
*GET, SL_constr_1, NODE, 0, NUM, MAX
csys, 0
NROTAT, ALL, LOCAL              ! Rotate all nodes to align with the local system

csys, 13
NSEL,S, LOC, X, NX-eps, NX+eps
NSEL,R, LOC, Z, NZ-eps, NZ+eps
NSEL,R, LOC, Y, y2*N+5-1, y2*N+5+1
*GET, SL_constr_2, NODE, 0, NUM, MAX
csys, 0
NROTAT, ALL, LOCAL              ! Rotate all nodes to align with the local system

TYPE, 2
E, SL_constr_1, SL_constr_2

csys, 13
! Select the nodes of the top plate to connect with the spiralift
NSEL,S, LOC, X, NX-30, NX+30
NSEL,R, LOC, Z, NZ-30, NZ+30
NSEL,R, LOC, Y, y2*N+PS-esp, y2*N+PS+esp
NSEL,U, LOC, Y, y2*N+PS+0.1, y2*N+PS+1000

csys, 0
NROTAT, ALL, LOCAL              ! Rotate all nodes to align with the local system

RBE3, SL_constr_2, ALL, ALL	! Distributes the force/moment applied at the master node to a set of slave nodes

allsel,all

CP, NEXT, ux, SL, SL_constr_1
CP, NEXT, uy, SL, SL_constr_1
CP, NEXT, uz, SL, SL_constr_1

||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||

! Create kassow joint node

csys,14
N, NEXT, 0, 0, 0
NSEL, S, LOC, Y, -eps, eps
NSEL, R, LOC, X, -eps, eps
NSEL, R, LOC, Z, -eps, eps
NROTAT, ALL, LOCAL              ! Rotate all nodes to align with the local system
*GET, KJ, NODE, 0, NUM, MIN

! Create end effector node for applying the force

csys,14
N, NEXT, -L, 0, 0
N, NEXT, -L, 0, -50

NSEL, S, LOC, Y, -eps, eps
NSEL, R, LOC, X, -L-eps, -L+eps
NSEL, R, LOC, Z, -eps, eps
NROTAT, ALL, LOCAL              ! Rotate all nodes to align with the local system
*GET, EE1, NODE, 0, NUM, MIN

NSEL, S, LOC, Y, -eps, eps
NSEL, R, LOC, X, -L-eps, -L+eps
NSEL, R, LOC, Z, -50-eps, -50+eps
NROTAT, ALL, LOCAL              ! Rotate all nodes to align with the local system
*GET, EE, NODE, 0, NUM, MIN
ALLSEL, ALL

! Create the kassow arm element
TYPE,2
E, KJ, EE1
E, EE1, EE

! Rotate the EE node to ensure that the y component is always vertical
NSEL,S,NODE,,EE
csys, 0
NROTAT, ALL, LOCAL              ! Rotate all nodes to align with the local system

! Connect the base nodes of the top plate to the K1 node
csys, 13
NSEL, S, LOC, X, -20, 20
NSEL, R, LOC, Y, y2*N+PS+TH_plate-eps, y2*N+PS+TH_plate+eps
NSEL, U, LOC, Z, -195.053-220, -195.053+220     ! 100
csys, 14
NROTAT, ALL, LOCAL              ! Rotate all nodes to align with the local system

RBE3, KJ, ALL, ALL	! Distributes the force/moment applied at the master node to a set of slave nodes

! Rotate the bottom nodes for constraints
csys, 12
NSEL, S, LOC, Z, z_shift - eps*5, z_shift + eps*5
NSEL, R, LOC, X, x_shift - eps*5, x_shift + eps*5
NSEL, R, LOC, y, -eps, eps
csys, 0
NROTAT, ALL, LOCAL              ! Rotate all nodes to align with the local system

csys, 12
NSEL, S, LOC, Z, z_shift - eps*5, z_shift + eps*5
NSEL, R, LOC, X, x2 - x_shift - eps*5, x2 - x_shift + eps*5
NSEL, R, LOC, y, -eps, eps
csys, 11
NROTAT, ALL, LOCAL              ! Rotate all nodes to align with the local system

allsel,all

Fini


||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||

/SOLUTION
ANTYPE, 4		! Set analysis type to transient
TIME,4                  ! Set total analysis time to 4 seconds
NSUBST,4	        ! Divide into 4 equal substeps
DELTIM,0.1              ! Set initial time step to 0.1 seconds
AUTOTS,ON               ! Enable automatic time stepping

NLGEOM,ON		! Non-linear analysis command

! >>>>> EXTERNAL CONSTRAINTS <<<<<

! Bottom first edge constraints
csys, 0
NSEL, S, LOC, Z, z_shift - eps*5, z_shift + eps*5
NSEL, R, LOC, X, x_shift - eps*5, x_shift + eps*5
NSEL, R, LOC, Y, - eps, eps
D,ALL,ALL

! Bottom second edge constraints
csys, 12
NSEL, S, LOC, Z, z_shift - eps*5, z_shift + eps*5
NSEL, R, LOC, X, x2 - x_shift - eps*5, x2 - x_shift + eps*5
NSEL, R, LOC, Y, - eps, eps
D, ALL, uy
D, ALL, uz
D, ALL, rotx
D, ALL, roty
D, ALL, rotz

! Bottom third edge constraints
csys, 12
NSEL, S, LOC, Z, z_shift - eps*5, z_shift + eps*5
NSEL, R, LOC, X, x_shift - eps*5, x_shift + eps*5
NSEL, R, LOC, Y, - eps, eps
D, ALL, uy
D, ALL, uz
D, ALL, rotx
D, ALL, roty
D, ALL, rotz

! Bottom side spiral lift constraint
csys, 13
NSEL,S,LOC,X,0
NSEL,R,LOC,Y,Spir_base
NSEL,R,LOC,Z,Spir_pos
*GET, SL_BOTTOM, NODE, 0, NUM, MIN  ! Spiral lift bottom node
D, ALL, ux
D, ALL, uy
D, ALL, uz
D, ALL, roty

! >>>>> FORCES AND MOMENTS <<<<<

allsel,all
TIME,0
F, EE, FY, -EQ_force/4
TIME,1
F, EE, FY, -EQ_force/2
TIME,2
F, EE, FY, -EQ_force*3/4
TIME,3
F, EE, FY, -EQ_force
TIME,4
F, EE, FY, -EQ_force

! Get element and node count
*GET, E_TOT_COUNT, ELEM,,COUNT
*GET, N_TOT_COUNT, NODE,,COUNT

/eshape,1

!Gravitational acceleration
Acel,,9.81	!m/s2

allsel,all
outres,all,all

solve
fini

/post1
set,last

csys, 13
NSEL, S, LOC, X, 325-40, 325+2
NSEL, R, LOC, Y, y2*N+PS+TH_plate-eps, y2*N+PS+TH_plate+eps
NSEL, R, LOC, Z, -195.053-3, -195.053+3